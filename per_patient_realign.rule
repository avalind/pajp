configfile: "config.json"

def _find_realignment_runtime(n_samples):
	"""
		Small heuristic for computing the needed
		core hours for per patient indel realignemnt.
	"""
	print(n_samples)
	base = 14
	if n_samples-2 >= 0:
		extra = (n_samples-2) * 8
	else:
		extra = 0
	return "{1}:00:00".format(base+extra)

rule picard_dedup:
	input:
		"{sample_name}/{sample_name}_merged.bam"
	output:
		temp("{sample_name}/{sample_name}.merged.dedup.bam"),
		"{sample_name}/{sample_name}.dedup_metrics.txt"
	params:
		java_args_picard=config["java_args_picard"],
		time="3:00:00"
	shell:
		"""
		module load bioinfo-tools java picard
		java {params.java_args_picard} -jar $PICARD_HOME/MarkDuplicates.jar I={input} O={output[0]} METRICS_FILE={output[1]}
		"""

rule samtools_index:
	input:
		"{file}.merged.dedup.bam"
	output:
		"{file}.merged.dedup.bam.bai"
	params:
		time="1:00:00"
	shell:
		"""
		module load bioinfo-tools samtools
		samtools index {input}
		"""

rule per_patient_realign_create_targets:
	input:
		bam_files=expand("{sample_name}/{sample_name}.merged.dedup.bam", sample_name=config["sample_names"]),
		bai_files=expand("{sample_name}/{sample_name}.merged.dedup.bam.bai", sample_name=config["sample_names"])
	output:
		temp("per_patient_realigned.target_intervals.list")
	params:
		java_args_gatk=config["java_args_gatk"],
		gatk_refpath=config["gatk_ref"],
		gold_indel_mills=config["gold_indel_mills"],
		gold_indel_1000g=config["gold_indel_1000g"],
		time="{0}:00:00".format(8*len(config["sample_names"]))
	run:
		input_string=""
		for bamfile in input.bam_files:
			input_string += " -I {0}".format(bamfile)
		
		shell(
		"module load bioinfo-tools java GATK && " + 
		"java {params.java_args_gatk} -jar $GATK_HOME/GenomeAnalysisTK.jar "+
		"-T RealignerTargetCreator "+
		"-R {params.gatk_refpath} "+
		"{input_string} "+
		"-known {params.gold_indel_mills} -known {params.gold_indel_1000g} "+
		"-o {output} ")
	
rule per_patient_realign_apply_targets:
	input:
		bam_files=expand("{sample_name}/{sample_name}.merged.dedup.bam", sample_name=config["sample_names"]),
		bai_files=expand("{sample_name}/{sample_name}.merged.dedup.bam.bai", sample_name=config["sample_names"]),
		target_intervals="per_patient_realigned.target_intervals.list"
	output:
		expand("{sample_name}.merged.dedup.per_patient_realigned.bam", sample_name=config["sample_names"])
	params:
		java_args_gatk=config["java_args_gatk"],
		gatk_refpath=config["gatk_ref"],
		gold_indel_mills=config["gold_indel_mills"],
		gold_indel_1000g=config["gold_indel_1000g"],
		time="{0}:00:00".format(8*len(config["sample_names"]))
	run:
		input_string=""
		for bamfile in input.bam_files:
			input_string += " -I {0}".format(bamfile)
		
		shell(
		"module load bioinfo-tools java GATK && "+
		"java {params.java_args_gatk} -jar $GATK_HOME/GenomeAnalysisTK.jar "+
		"-T IndelRealigner "+
		"-R {params.gatk_refpath} "+
		"-known {params.gold_indel_mills} -known {params.gold_indel_1000g} "+
		"-targetIntervals {input.target_intervals} "+
		"{input_string} "+
		"-nWayOut '.per_patient_realigned.bam'")

